%{
    
#import <Foundation/Foundation.h>
#import "y.tab.h"

#define NSSTRINGR(value) (__bridge CFTypeRef)[NSString stringWithFormat:@"%s", value]


CFTypeRef removeFirstAndLast(const char *value);

%}

%s COMMENT

whitespace  [ \t\r\n]
rulename    \<[^<>]+\>
tag         \{(\\.|[^\}]+)*\}
weight      \/[0-9]*(\.[0-9]+)?(e-)?[0-9]*\/
token       [^ \t\r\n=;|*+<>()\[\]{}*/]+
quotedtoken \"(\\.|[^"]+)*\"

%%


{whitespace} ;                          /* ignore whitespace */

<INITIAL>\/\/.*\n {};                   /* single-line comments */

<INITIAL>\/\* { BEGIN(COMMENT);}        /* multi-line comments */
<COMMENT>\*\/ { BEGIN(INITIAL);}
<COMMENT>. ;

<INITIAL>#JSGF {return HEADER;}
<INITIAL>grammar {return GRAMMAR;}
<INITIAL>import {return IMPORT;}
<INITIAL>public {return PUBLIC;}

<INITIAL>{rulename} {yylval.name=NSSTRINGR(yytext);  return RULENAME;}

<INITIAL>{tag} {yylval.name=NSSTRINGR(yytext);  return TAG;}
<INITIAL>{token} {yylval.name=NSSTRINGR(yytext); return TOKEN;}
<INITIAL>{quotedtoken} {yylval.name=NSSTRINGR(yytext);  return QUOTEDTOKEN;}
<INITIAL>{weight} {yylval.weight=atof(yytext+1); return WEIGHT;}

<INITIAL>.   {return yytext[0];}    /* rest */

%%

int yywrap()
{
    // This is a place where one can append more text if it would be needed, quit for now
    return 1;
}
